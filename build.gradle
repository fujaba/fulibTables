plugins {
   id 'java-library'
   id 'maven-publish'
   // https://plugins.gradle.org/plugin/com.jfrog.bintray
   id 'com.jfrog.bintray' version '1.8.5'
   // https://plugins.gradle.org/plugin/de.clashsoft.simple-publish
   id 'de.clashsoft.simple-publish' version '0.6.0'
   // https://plugins.gradle.org/plugin/com.palantir.revapi
   id 'com.palantir.revapi' version '1.4.3'
}

// --------------- General Info ---------------

group = 'org.fulib'
version = 'git describe --tags'.execute().text[1..-2] // strip v and \n
description = 'Fulib Tables are the model query and transformation mechanisms provided by Fulib.'

publishInfo {
   organization = 'fujaba'
   githubRepo = 'fujaba/fulibTables'
   labels = [ 'fulib', 'model', 'query', 'transformation', 'tables' ]

   developer {
      id = 'zuendorf'
      name = 'Albert Zuendorf'
   }
   developer {
      id = 'digitalhoax'
      name = 'Tobias George'
   }
   developer {
      id = 'eicke123'
      name = 'Christoph Eickhoff'
   }

   license {
      name = 'MIT License'
      url = 'http://www.opensource.org/licenses/mit-license.php'
   }
}

sourceCompatibility = 1.8

sourceSets.create('gen')

// --------------- Dependencies ---------------

repositories {
   mavenLocal()
   mavenCentral()
   jcenter()
}

dependencies {
   // https://mvnrepository.com/artifact/org.fulib/fulibYaml
   api group: 'org.fulib', name: 'fulibYaml', version: '1.3.0'

   // https://mvnrepository.com/artifact/junit/junit
   testImplementation group: 'junit', name: 'junit', version: '4.13'

   // https://mvnrepository.com/artifact/org.fulib/fulib
   def fulib = [ group: 'org.fulib', name: 'fulib', version: '1.2.0' ]

   // https://mvnrepository.com/artifact/org.fulib/fulibTools
   def fulibTools = [ group: 'org.fulib', name: 'fulibTools', version: '1.2.0' ]

   genCompile fulib
   genCompile fulibTools
   genCompile group: 'org.antlr', name: 'ST4', version: '4.3'

   testImplementation fulib
   testImplementation fulibTools
}

// --------------- Misc. Settings ---------------

revapi {
   oldVersion = '1.1.0'
}

tasks.named('test') {
   def codeFragmentFiles = fileTree(it.workingDir).include('**/*.java', '**/*.md', 'build.gradle')
   it.inputs.files(codeFragmentFiles)
   // enabling those makes cleanTest delete all java and markdown files :O
   // it.outputs.files(codeFragmentFiles)
   // it.outputs.dir('doc')
}
