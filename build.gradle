plugins {
   id 'java-library'
   id 'maven-publish'
   // https://plugins.gradle.org/plugin/com.jfrog.bintray
   id 'com.jfrog.bintray' version '1.8.5'
   // https://plugins.gradle.org/plugin/de.clashsoft.simple-publish
   id 'de.clashsoft.simple-publish' version '0.6.0'
   // https://plugins.gradle.org/plugin/com.palantir.revapi
   id 'com.palantir.revapi' version '1.4.3'
   // https://plugins.gradle.org/plugin/org.fulib.fulibGradle
   id 'org.fulib.fulibGradle' version '0.5.0'
}

// --------------- General Info ---------------

group = 'org.fulib'
version = 'git describe --tags'.execute().text[1..-2] // strip v and \n
description = 'Fulib Tables are the model query and transformation mechanisms provided by Fulib.'

publishInfo {
   organization = 'fujaba'
   githubRepo = 'fujaba/fulibTables'
   labels = [ 'fulib', 'model', 'query', 'transformation', 'tables' ]

   developer {
      id = 'zuendorf'
      name = 'Albert Zuendorf'
   }
   developer {
      id = 'digitalhoax'
      name = 'Tobias George'
   }
   developer {
      id = 'eicke123'
      name = 'Christoph Eickhoff'
   }

   license {
      name = 'MIT License'
      url = 'http://www.opensource.org/licenses/mit-license.php'
   }
}

sourceCompatibility = 1.8

// --------------- Dependencies ---------------

repositories {
   mavenLocal()
   mavenCentral()
}

dependencies {
   // https://mvnrepository.com/artifact/org.fulib/fulibScenarios
   fulibScenarios group: 'org.fulib', name: 'fulibScenarios', version: '1.6.2'

   // https://mvnrepository.com/artifact/org.fulib/fulibYaml
   api group: 'org.fulib', name: 'fulibYaml', version: '1.5.0'

   // https://mvnrepository.com/artifact/junit/junit
   testImplementation group: 'junit', name: 'junit', version: '4.13'

   // https://mvnrepository.com/artifact/org.antlr/ST4
   genCompile group: 'org.antlr', name: 'ST4', version: '4.3.1'

   // https://mvnrepository.com/artifact/org.fulib/fulibTools
   testImplementation group: 'org.fulib', name: 'fulibTools', version: '1.5.0'
}

// --------------- Misc. Settings ---------------

revapi {
   oldVersion = '1.4.0'
}

tasks.named('test') {
   def codeFragmentFiles = fileTree(it.workingDir).include('**/*.java', '**/*.md', 'build.gradle')
   it.inputs.files(codeFragmentFiles)
   // enabling those makes cleanTest delete all java and markdown files :O
   // it.outputs.files(codeFragmentFiles)
   // it.outputs.dir('doc')
}
